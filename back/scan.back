8 "scan_IRheight" store
10 "catScan_dropHeight" store
100 "pileSwitchThreshold" store

7 "scan_catScanLimit" store
1 "scan_catPickLimit" store

leftOrRightArm "right" =
( 
-0.350000 -0.568750 0.20 -0.001554 0.999990 0.003099 -0.002801 createEEPose "workspace1" store
-0.068750 -0.712500 0.20 -0.001554 0.999990 0.003099 -0.002801 createEEPose "workspace2" store
0.281250 -0.741250 0.20 -0.001554 0.999990 0.003099 -0.002801 createEEPose  "workspace3" store
0.575000 -0.368750 0.201245 -0.001554 0.999990 0.003099 -0.002801 createEEPose "workspace4" store )
( 
leftOrRightArm "left" = 
( -0.350000 0.568750 0.20 -0.001554 0.999990 0.003099 -0.002801 createEEPose "workspace1" store
-0.068750 0.712500 0.20 -0.001554 0.999990 0.003099 -0.002801 createEEPose "workspace2" store
0.281250 0.741250 0.20 -0.001554 0.999990 0.003099 -0.002801 createEEPose  "workspace3" store
0.575000 0.368750 0.201245 -0.001554 0.999990 0.003099 -0.002801 createEEPose "workspace4" store  )
 ( "Neither left or right arm." leftOrRighArm pauseStackExecution ) 
ifte
)
ifte


workspace1 "inputPileWorkspace" store
workspace3 "playWorkspace" store
workspace4 "outputPileWorkspace" store

"catScan_" robotSerial "_" leftOrRightArm "_" dateString + + + + + "scan_catScanObjectName" store

"utils" import
"wiki" import

"clearWorkspace" import
"scene" import

(
  70 30 fixCameraLightingExposureGain
  { |B 
    0.1 "gazeDelta" store
    { |B 
      ( 1 ) ( 
	blink_lightshow 
      ) while 
    } 
    { |B 
      ( 
	playWorkspace catScan2 
      ) 5 replicateWord 
    } 
    { |B 
      ( 1 ) ( 
	drand48 -0.5 plus 4.0 times 5 setHeadPanTargetSpeed 
	drand48 20 times spinForSeconds 
	endStackCollapseNoop 
      ) while 
    } 
    { |B 
      ( 1 ) ( 
	publishWristViewToFace
      ) while 
    } 
    { |B 
      ( 1 ) ( 
	isGripperGripping ( 
	  nod 
	) ift 
      ) while 
    } 
  } ( 1 ) ( slip ) while
) "scan_threaded_catScan_wristFace" store

(
  70 30 fixCameraLightingExposureGain
  { |B 
    0.1 "gazeDelta" store
    { |B 
      ( 1 ) ( 
	blink_lightshow 
      ) while 
    } 
    { |B 
      ( 
	playWorkspace catScan2 
      ) 5 replicateWord 
    } 
    { |B 
      ( 1 ) ( 
	drand48 -0.5 plus 4.0 times 5 setHeadPanTargetSpeed 
	drand48 20 times spinForSeconds 
	endStackCollapseNoop 
      ) while 
    } 
    { |B 
      ( 1 ) ( 
	isGripperGripping ( 
	  nod 
	) ift 
      ) while 
    } 
  } ( 1 ) ( slip ) while
) "scan_threaded_catScan_noFace" store

(
  70 30 fixCameraLightingExposureGain
  { |B 
    0.1 "gazeDelta" store
    { |B 
      ( 1 ) ( 
	blink_lightshow 
      ) while 
    } 
    { |B 
      ( 
	playWorkspace catScan2 
      ) 5 replicateWord 
    } 
    { |B 
      ( 1 ) ( 
	drand48 -0.5 plus 4.0 times 5 setHeadPanTargetSpeed 
	drand48 20 times spinForSeconds 
	endStackCollapseNoop 
      ) while 
    } 
    { |B 
      ( 1 ) ( 
	30006 "gazeNumber" store
	"gaze" gazeNumber ".tif" + + publishImageFileToFace 
	gazeDelta spinForSeconds

	(
	  gazeNumber 1 + "gazeNumber" store
	  "gaze" gazeNumber ".tif" + + publishImageFileToFace 
	  gazeDelta spinForSeconds
	) 6 replicateWord

	(
	  gazeNumber -1 + "gazeNumber" store
	  "gaze" gazeNumber ".tif" + + publishImageFileToFace 
	  gazeDelta spinForSeconds
	) 5 replicateWord
	endStackCollapseNoop 
      ) while 
    } 
    { |B 
      ( 1 ) ( 
	isGripperGripping ( 
	  nod 
	) ift 
      ) while 
    } 
  } ( 1 ) ( slip ) while
) "scan_threaded_catScan_aFace" store

(
  { |B 
    ( 1 ) ( 
      drand48 -0.5 plus 4.0 times 5 setHeadPanTargetSpeed 
      drand48 20 times spinForSeconds 
      drand48 0.5 > ( 
	"ursula_yes.tif" publishImageFileToFace 
      ) ( 
	"ursula_no.tif" publishImageFileToFace 
      ) ifte 
      endStackCollapseNoop 
    ) while 
  } 
  { |B 
    ( 1 ) ( 
      30006 "gazeNumber" store
      "gaze" gazeNumber ".tif" + + publishImageFileToFace 
      gazeDelta spinForSeconds

      (
	gazeNumber 1 + "gazeNumber" store
	"gaze" gazeNumber ".tif" + + publishImageFileToFace 
	gazeDelta spinForSeconds
      ) 6 replicateWord

      (
	gazeNumber -1 + "gazeNumber" store
	"gaze" gazeNumber ".tif" + + publishImageFileToFace 
	gazeDelta spinForSeconds
      ) 5 replicateWord
      endStackCollapseNoop 
    ) while 
  } 
) "scan_thread_alterface" store

(
  clearMapForPatrol
  synchronicServo gradientServoPrep
  buildClassSimilarityMatrixFromDensity
  0.8 > (
    0.0
    " **** not a new component " print
  ) (
    1.0
    " **** is a new component " print
  ) ifte
) "scan_isNewComponent" store


(
  setIdleModeToEmpty
  openGripper
  
  ( 1 changeToHeight scan_isNewComponent ! ) (
    pickFocusedClass

    isGripperGripping (
      playWorkspace moveEeToPoseWord
      comeToStop pressUntilEffortInit 0.03 setSpeed pressUntilEffort quarterImpulse 0.01 setGridSize ( zUp ) catScan_dropHeight setMovementStateToMoving replicateWord comeToStop 
    ) ift
    openGripper

  ) while

) "scan_findNewComponent" store

(
  playWorkspace moveEeToPoseWord 
  tempUpdateBg
  playWorkspace cw_clearWorkspace
  openGripper waitUntilGripperNotMoving
  ">>>> entering pickFromInputPile <<<<" print

  ( isGripperGripping ! ) (
    /*
    endArgs "kr_dash2" setClassLabels createCachedClassifierFromClassLabels "kr_dash2" setTargetClass setPickModeToStaticMarginals
    2 setGradientServoHardMaxIterations
    2 setGradientServoSoftMaxIterations
    */
    scan_swapToBars

    0 "pileSwitchCounter" store

    ( isGripperGripping ! ) (
      ">>>> re-entering pickFromInputPile <<<<" print
      openGripper
      halfImpulse
      inputPileWorkspace moveEeToPoseWord waitUntilAtCurrentPosition
      ( perturbPosition ) 5 replicateWord 
      1 changeToHeight 
      waitUntilAtCurrentPosition
      clearMapForPatrol clearBlueBoxMemories mapLocal
      deliverTargetObject
      liftArm
      pileSwitchCounter 1 + "pileSwitchCounter" store
      pileSwitchCounter pileSwitchThreshold > ( 
	"  >>>> switching piles " print
	inputPileWorkspace "temp" store 
	outputPileWorkspace "inputPileWorkspace" store
	temp "outputPileWorkspace" store
	0 "pileSwitchCounter" store
      ) ift
    ) while

    halfImpulse
    playWorkspace moveEeToPoseWord
    waitUntilAtCurrentPosition
    ( zDown ) 15 replicateWord

    isGripperGripping ! ( playWorkspace cw_clearWorkspace ) ift 
  ) while
) "pickFromInputPile" store

( 
  ">>>> entering playWithObject <<<<" print
  comeToStop pressUntilEffortInit 0.03 setSpeed pressUntilEffort openGripper
  stay 
  0.01 setGridSize
  ( zUp ) scan_IRheight replicateWord
  comeToStop
  setScanModeCentered
  currentPose
  catScan5
) "playWithObject" store


( 
  moveEeToPoseWord
  waitUntilAtCurrentPosition

  0 "scan_pointCounter" store
  0 "scan_breakInd" store

  /* 0.01 setGridSize ( xDown ) 20 replicateWord */
  0.01 setGridSize ( xDown ) 10 replicateWord
  ( yDown ) 5 replicateWord

  ( scan_pointCounter 3 /* 4 */ <   scan_breakInd not   and ) (
    ( yUp ) 10 replicateWord
    cw_viewIsClear (
      ( yDown ) 10 replicateWord
      cw_viewIsClear (
	( xUp ) 10 replicateWord
      ) (
	1 "scan_breakInd" store
      ) ifte
    ) (
      1 "scan_breakInd" store
    ) ifte

    scan_pointCounter 1 + "scan_pointCounter" store
  ) while

  scan_breakInd
) "scan_findObjectAfterDrop_old" store

( 
  tempSwapToBlocks

  0 "scan_breakInd" store

  moveEeToPoseWord waitUntilAtCurrentPosition
  1 changeToHeight

  catScan5LoadPlayBg
  tempSpiralTakeScene
  
  "scan_findObjectAfterDrop: " print
      cw_clearWorkspace_discrepancy_thresh_val sceneCountDiscrepantCells " discrepant cells at val threshold " 
      cw_clearWorkspace_discrepancy_thresh_val " cell threshold " cw_clearWorkspace_discrepancy_thresh_cells 
    + + + +
  print

  cw_clearWorkspace_discrepancy_thresh_val sceneCountDiscrepantCells cw_clearWorkspace_discrepancy_thresh_cells < (
    "Found no object." print
    0 "scan_breakInd" store
  ) (
    "Found an object, moving to it." print
    1 "scan_breakInd" store
    tempSpiralServoToAnonymousObject waitUntilAtCurrentPosition
  ) ifte

  scan_breakInd

  tempRestoreFromBlocks
) "scan_findObjectAfterDrop" store

(
  slide ( pushClassLabels 1 sP ) "scan_swap_classLabels" store
  focusedClassLabel "scan_swap_targetClass" store
  setPickModeToStaticMarginals
  /* endArgs "kr_bars/10_5_2" "kr_bars/10_5_4" "kr_bars/10_5_6" "kr_bars/10_5_8" "kr_bars/10_5_10" setClassLabels */
  endArgs "kr_bars/10_4_0" "kr_bars/10_5_2" "kr_bars/10_5_4" "kr_bars/10_5_6" "kr_bars/10_5_8" "kr_bars/10_5_10" setClassLabels
  "kr_bars/10_5_2" setTargetClass
  /* endArgs "kr_bars/10_5_6" "kr_bars/10_5_8" "kr_bars/10_5_10" setClassLabels
  "kr_bars/10_5_6" setTargetClass */
  createCachedClassifierFromClassLabels
  1 setGradientServoMode
  setGraspModeToCrane

  2 setGradientServoSoftMaxIterations 2 setGradientServoHardMaxIterations 1 setMappingServoTimeout 
) "scan_swapToBars" store

(
  endArgs scan_swap_classLabels setClassLabels 
  scan_swap_targetClass setTargetClass
  createCachedClassifierFromClassLabels
  0 setGradientServoMode
  setGraspModeTo3D
) "scan_restoreFromBars" store

(
  clearMapForPatrol

  /*
  start3dGraspAnnotationNoChange 
  c3dPoseBase "scan_catScan2_base" store 
  */
  lock3dGraspBase c3dPoseBase eePosePZ "scan_catScan2_basePZ" store
  synchronicServo
  gradientServo
  currentPose scan_catScan2_basePZ setEEPosePZ "scan_catScan2_base" store

  scan_swapToBars
  /* 
  start3dGraspAnnotationNoChange 
  */
  gradientServo
  prepareForGraspFromMemory
  currentPose scan_catScan2_basePZ pickFlushFactor + setEEPosePZ "scan_catScan2_newGrasp" store
  scan_restoreFromBars

  scan_catScan2_base setC3dPoseBase
  scan_catScan2_newGrasp add3dGraspPoseWord
  writeFocusedClass
) "scan_addBar3dGrasp" store

(
  ( 1 ) (
    setPlaceModeToShake
    1 changeToHeight
    clearMapForPatrol clearBlueBoxMemories
    tempMapBestAndPick

    playWorkspace moveEeToPoseWord 1 changeToHeight currentPose
    scan_findObjectAfterDrop "scan_catScanSees" store
    scan_catPickCounter 1 + "scan_catPickCounter" store
  ) while
) "catPlayLoop" store

(
  setPickModeToLearningAlgorithmC
  1 setSnapToFlushGrasp
  2 setGradientServoHardMaxIterations
  2 setGradientServoSoftMaxIterations

  1 "somethingsThere" store

  ( isGripperGripping ! somethingsThere and ) (
    ">>>> entering moveToOutputPile <<<<" print
    halfImpulse playWorkspace moveEeToPoseWord waitUntilAtCurrentPosition
    ( perturbPosition ) 5 replicateWord 
    openGripper
    halfImpulse
    1 changeToHeight
    setPlaceModeToHold

    playWorkspace cw_workspaceIsClear ( 0 "somethingsThere" store ) ift

    "somethingsThere: " print somethingsThere print

    1 changeToHeight
    pickFocusedClass
    isGripperGripping ( liftArm ) ( openGripper playWorkspace cw_clearWorkspace ) ifte
  ) while

  somethingsThere isGripperGripping and (
    halfImpulse outputPileWorkspace moveEeToPoseWord waitUntilAtCurrentPosition
    isGripperGripping ( comeToStop pressUntilEffortInit 0.03 setSpeed pressUntilEffort openGripper liftArm ) ift 
  ) ( openGripper ) ifte

  playWorkspace cw_clearWorkspace
) "moveToOutputPile" store

(
  1 setSnapToFlushGrasp
  setPlaceModeToHold
  setBoundingBoxModeToMapping
  clearMapForPatrol
  100 setGripperMovingForce
  openGripper

  ( 1 )
  (
    pickFromInputPile2
    playWithObject
    moveToOutputPile2
  ) while

) "infiniteScan" store


5 "catScan5NumVarianceTrials" store
dateString "catScan5VarianceTrialBatchTime" store
dateString "catScan5VarianceTrialThisTime" store
"trial" "catScan5VarianceTrialName" store
(
  dataDirectory "/objects/" scan_catScanObjectName "/" catScan5VarianceTrialName "/" + + + + + "catScan5VarianceTrialDir" store
  catScan5VarianceTrialDir mkdirs

  "catScan5VarianceTrial BEGINNING" print

  0.1 perturbPositionScale waitUntilAtCurrentPosition

  sceneClearPredictedObjects tempUpdateMaps 

    ( tempSpiralServoToBestSceneObject waitUntilAtCurrentPosition setMovementStateToMoving comeToStop ) 3 replicateWord 

    scenePushNumSceneObjects
    1
  = 
  (
     dateString
  "catScan5VarianceTrialThisTime" store

  catScan5VarianceTrialDir "scene_" catScan5VarianceTrialBatchTime "_" catScan5VarianceTrialThisTime + + + + 
  "catScan5VarianceTrialThisFileName" store

  sceneSetAnnotatedClassNameToFocusedClass
  sceneSetPredictedClassNameToFocusedClass

    catScan5VarianceTrialThisFileName ".yml" +
  sceneSaveSceneAbsolute

  clearBlueBoxMemories 

    0 
  sceneMapSceneObject
  ) ift

  "catScan5VarianceTrial ENDING" print
) "catScan5VarianceTrial" store

( 
    /* string argument on stack */
  "catScan5VarianceTrialName" store

  halfImpulse 
  ( 
    tempShortServoToBestSceneObject waitUntilAtCurrentPosition setMovementStateToMoving comeToStop 
  ) 3 replicateWord

    currentPose 
  ( 
    catScan5VarianceTrial dup moveEeToPoseWord waitUntilAtCurrentPosition setMovementStateToMoving comeToStop 
  ) 5 replicateWord
  pop
) "catScan5VarianceTrials" store

( /* expects an eePose on the stack */
  clearClassLabels
  setGraspModeTo3D

  tenthImpulse

    /* eePose */
  moveEeToPoseWord 1 changeToHeight waitUntilAtCurrentPosition

    0 
  "scan_catScanCounter" store

    "catScan5_" robotSerial leftOrRightArm dateString "/" + + + + 
  "scan_catScanObjectName" store
    scan_catScanObjectName 
  setScanGroup
  
      currentPose
    scan_findObjectAfterDrop 
  "scan_catScanSees" store

  setBoundingBoxModeToMapping
  clearMapForPatrol
  1 changeToHeight waitUntilAtCurrentPosition

  initializeAndFocusOnTempClass
  tempRecaptureServoSpiral
  clearClassLabels

    0 currentTableZ - pickFlushFactor + 
  "scan_gripperTouchTableZ" store

      currentPose 
      scan_gripperTouchTableZ 
    setEEPosePZ 
  moveEeToPoseWord

  0.01 setGridSize 
  ( 
    zUp 
  ) scan_IRheight replicateWord 
  comeToStop 

  setPickModeToLearningAlgorithmC

  ( scan_catScanCounter scan_catScanLimit <   scan_catScanSees   and ) (

    1 changeToHeight 
    currentPose "catScan5Pos" store

    tempSpiralIsNewComponentNormalizedSum (

      catScan5Pos moveEeToPoseWord waitUntilAtCurrentPosition
      tempFullScan
      catScan5Pos moveEeToPoseWord waitUntilAtCurrentPosition

/*
      integrateImageStreamBufferCrops
*/
      createCachedClassifierFromClassLabels
      trainAndWriteFocusedClassKnn

      1 changeToHeight
      waitUntilAtCurrentPosition

      clearClass3dGrasps
      writeFocusedClass

      tempAddBlockGrasp

      setPlaceModeToHold
      clearMapForPatrol

      tempMapBestAndPick

	playWorkspace 
      moveEeToPoseWord waitUntilAtCurrentPosition

      isGripperGripping (
	currentPose scan_gripperTouchTableZ setEEPosePZ moveEeToPoseWord
	quarterImpulse 0.01 setGridSize ( zUp ) catScan_dropHeight replicateWord setMovementStateToMoving comeToStop 
      ) ift
      openGripper
    ) (

      /* sets the target class */
      setPlaceModeToHold
      clearMapForPatrol

      tempAddBlockGrasp
      setPickModeToLearningAlgorithmC

      /* tempPickWithAddedGrasp */
      tempMapFocusedAndPick

      setGridSizeCoarse departureSpeed ( zUp ) 10 replicateWord setMovementStateToMoving comeToStop

      isGripperGripping (
	playWorkspace moveEeToPoseWord

	currentPose scan_gripperTouchTableZ setEEPosePZ moveEeToPoseWord
	quarterImpulse 0.01 setGridSize ( zUp ) catScan_dropHeight replicateWord setMovementStateToMoving comeToStop 
      ) ift
      openGripper
    ) ifte

    1 changeToHeight

	currentPose
      scan_findObjectAfterDrop 
    "scan_catScanSees" store
    currentPose "catScan5Pos" store

      scan_catScanCounter 1 + 
    "scan_catScanCounter" store

  ) while 

  setPickModeToLearningAlgorithmC
  setPlaceModeToShake

    0 
  "scan_catPickCounter" store

    playWorkspace 
  moveEeToPoseWord 
  1 changeToHeight 

      currentPose
    scan_findObjectAfterDrop 
  "scan_catScanSees" store

  ( scan_catPickCounter scan_catPickLimit <   scan_catScanSees   and ) (
    1 changeToHeight
    clearMapForPatrol clearBlueBoxMemories

    dateString "catScan5VarianceTrialBatchTime" store
    ( 
      catScan5VarianceTrial 
    ) catScan5NumVarianceTrials replicateWord

    tempMapFocusedAndPick

      playWorkspace 
    moveEeToPoseWord 
    1 changeToHeight 

	currentPose
      scan_findObjectAfterDrop 
    "scan_catScanSees" store

      scan_catPickCounter 1 + 
    "scan_catPickCounter" store
  ) while

  /*
    catScan5 uses bandits over 3d grasps to select the best grasp, perform
    redundant servoing and collect data for localization and classification studies
  */
) "catScan5" store

(
  playWorkspace moveEeToPoseWord 
  catScan5UpdatePlayBg
  playWorkspace cw_clearWorkspace
  openGripper waitUntilGripperNotMoving
  ">>>> entering pickFromInputPile2 <<<<" print

  ( isGripperGripping ! ) (
    tempSwapToBlocks

    0 "pileSwitchCounter" store

    ( isGripperGripping ! ) (
      ">>>> re-entering pickFromInputPile2 <<<<" print
      openGripper
      halfImpulse
      inputPileWorkspace moveEeToPoseWord 1 changeToHeight waitUntilAtCurrentPosition

      catScan5LoadInputBg
      catScan5InputPileTakeScene
      quarterImpulse
      tempSpiralServoToBestSceneObject waitUntilAtCurrentPosition
      setPickModeToStaticMarginals tempMapBestAndPick
      
      /* 
	clearMapForPatrol clearBlueBoxMemories mapLocal
	deliverTargetObject
      */

      liftArm
      pileSwitchCounter 1 + "pileSwitchCounter" store
      pileSwitchCounter pileSwitchThreshold > ( 
	"  >>>> switching piles " print
	inputPileWorkspace "temp" store 
	outputPileWorkspace "inputPileWorkspace" store
	temp "outputPileWorkspace" store
	0 "pileSwitchCounter" store
      ) ift
    ) while

    halfImpulse
    playWorkspace moveEeToPoseWord
    waitUntilAtCurrentPosition
    ( zDown ) 15 replicateWord

    isGripperGripping ! ( playWorkspace cw_clearWorkspace ) ift 
  ) while
) "pickFromInputPile2" store

4 "moveToOutputPile2MaxPicks" store
(
  setPickModeToStaticMarginals
  1 setSnapToFlushGrasp

  1 "somethingsThere" store
  0 "violatedMax" store
  1 "moveToOutputPile2ThisPick" store
  

  ( isGripperGripping ! somethingsThere violatedMax ! and and ) (

	">>>> entering moveToOutputPile2 <<<< with iteration " 
        moveToOutputPile2ThisPick 
      +
    print

    setPlaceModeToHold
    openGripper
    halfImpulse playWorkspace moveEeToPoseWord 1 changeToHeight waitUntilAtCurrentPosition

    cw_playWorkspaceIsClear ! "somethingsThere" store
    1 changeToHeight waitUntilAtCurrentPosition

    "somethingsThere: " print somethingsThere print
    somethingsThere (
      setPickModeToStaticMarginals tempMapBestAndPick
    ) (
    ) ifte

    isGripperGripping ( 
      liftArm 
    ) ( 
      /* oops didn't get it */
      openGripper
    ) ifte

    
    moveToOutputPile2ThisPick 1 + "moveToOutputPile2ThisPick" store

    moveToOutputPile2ThisPick moveToOutputPile2MaxPicks > (
      1 "violatedMax" store
      liftArm 
    ) ift 

  ) while

  somethingsThere isGripperGripping and (
    halfImpulse outputPileWorkspace moveEeToPoseWord waitUntilAtCurrentPosition waitUntilEndpointCallbackReceived
    isGripperGripping ( 
      comeToStop pressUntilEffortInit 0.03 setSpeed pressUntilEffort openGripper liftArm 
    ) ( 
      openGripper
    ) ifte
  ) ( 
    openGripper 
  ) ifte

  playWorkspace cw_clearWorkspace
) "moveToOutputPile2" store

(
  0.04 "tempSpiralMoveSpeed" store
  inputPileWorkspace moveEeToPoseWord waitUntilAtCurrentPosition 1 changeToHeight
  2 6 4 tempUpdateSpiralEccentricCore tempUpdateMaps
  "catScan5InputBg" "_" leftOrRightArm + + sceneSaveObservedMap
  catScan5LoadInputBg
) "catScan5UpdateInputBg" store

(
  "catScan5InputBg" "_" leftOrRightArm + + sceneLoadBackgroundMap
  tempRegularizeBackground
) "catScan5LoadInputBg" store

(
  0.04 "tempSpiralMoveSpeed" store
  playWorkspace moveEeToPoseWord 1 changeToHeight waitUntilAtCurrentPosition
  6 2 4 tempUpdateSpiralEccentricCore tempUpdateMaps
  "catScan5PlayBg" "_" leftOrRightArm + + sceneSaveObservedMap
  catScan5LoadPlayBg
) "catScan5UpdatePlayBg" store

(
  "catScan5PlayBg" "_" leftOrRightArm + + sceneLoadBackgroundMap
  tempRegularizeBackground
) "catScan5LoadPlayBg" store

(
  tempSpiralMoveSpeedDefault "tempSpiralMoveSpeed" store
  tempSpiralGridSizeDefault "tempSpiralGridSize" store
  1 5 4 tempUpdateSpiralEccentricCore tempUpdateMaps
) "catScan5InputPileTakeScene" store

(
  0.01 setGridSize ( xDown ) 4 replicateWord
  tempSpiralMoveSpeedDefault "tempSpiralMoveSpeed" store
  tempSpiralGridSizeDefault "tempSpiralGridSize" store
  5 1 4 tempUpdateSpiralEccentricCore tempUpdateMaps
) "catScan5PlayTakeScene" store















