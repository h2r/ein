8 "scan_IRheight" store
10 "catScan_dropHeight" store

leftOrRightArm "right" =
( 
-0.350000 -0.568750 0.20 -0.001554 0.999990 0.003099 -0.002801 createEEPose "workspace1" store
-0.068750 -0.712500 0.20 -0.001554 0.999990 0.003099 -0.002801 createEEPose "workspace2" store
0.281250 -0.741250 0.20 -0.001554 0.999990 0.003099 -0.002801 createEEPose  "workspace3" store
0.575000 -0.368750 0.201245 -0.001554 0.999990 0.003099 -0.002801 createEEPose "workspace4" store )
( 
leftOrRightArm "left" = 
( -0.350000 0.568750 0.20 -0.001554 0.999990 0.003099 -0.002801 createEEPose "workspace1" store
-0.068750 0.712500 0.20 -0.001554 0.999990 0.003099 -0.002801 createEEPose "workspace2" store
0.281250 0.741250 0.20 -0.001554 0.999990 0.003099 -0.002801 createEEPose  "workspace3" store
0.575000 0.368750 0.201245 -0.001554 0.999990 0.003099 -0.002801 createEEPose "workspace4" store  )
 ( "Neither left or right arm." leftOrRighArm pauseStackExecution ) 
ifte
)
ifte


workspace1 "inputPileWorkspace" store
workspace3 "playWorkspace" store
workspace4 "outputPileWorkspace" store
100 "pileSwitchThreshold" store

"catScan_" robotSerial "_" leftOrRightArm "_" dateString + + + + + "scan_catScanObjectName" store

"utils" import

"clearWorkspace" import

(
  clearMapForPatrol
  synchronicServo gradientServoPrep
  buildClassSimilarityMatrixFromDensity
  0.8 > (
    0.0
    " **** not a new component " print
  ) (
    1.0
    " **** is a new component " print
  ) ifte
) "scan_isNewComponent" store

(
    ( /* optionally
    setPlaceModeToHold
    clearMapForPatrol clearBlueBoxMemories mapLocal
    deliverTargetObject
    */ ) "aComment" store
) "aComment" store

(
  setIdleModeToEmpty
  openGripper
  
  ( 1 changeToHeight scan_isNewComponent ! ) (
    pickFocusedClass

    isGripperGripping (
      playWorkspace moveEeToPoseWord
      comeToStop pressUntilEffortInit 0.03 setSpeed pressUntilEffort 0.01 setGridSize ( zUp ) catScan_dropHeight replicateWord comeToStop 
    ) ift
    openGripper

  ) while

) "scan_findNewComponent" store

(
  playWorkspace cw_clearWorkspace
  openGripper waitUntilGripperNotMoving
  ">>>> entering pickFromInputPile <<<<" print

  ( isGripperGripping ! ) (
    endArgs "kr_dash2" setClassLabels createCachedClassifierFromClassLabels "kr_dash2" setTargetClass setPickModeToStaticMarginals
    2 setGradientServoHardMaxIterations
    2 setGradientServoSoftMaxIterations

    0 "pileSwitchCounter" store

    ( isGripperGripping ! ) (
      ">>>> re-entering pickFromInputPile <<<<" print
      openGripper
      halfImpulse
      inputPileWorkspace moveEeToPoseWord waitUntilAtCurrentPosition
      ( perturbPosition ) 5 replicateWord 
      1 changeToHeight 
      waitUntilAtCurrentPosition
      clearMapForPatrol clearBlueBoxMemories mapLocal
      deliverTargetObject
      liftArm
      pileSwitchCounter 1 + "pileSwitchCounter" store
      pileSwitchCounter pileSwitchThreshold > ( 
	"  >>>> switching piles " print
	inputPileWorkspace "temp" store 
	outputPileWorkspace "inputPileWorkspace" store
	temp "outputPileWorkspace" store
	0 "pileSwitchCounter" store
      ) ift
    ) while

    halfImpulse
    playWorkspace moveEeToPoseWord
    waitUntilAtCurrentPosition
    ( /* this is where the scanning loop starts */ ) "aComment" store
    ( zDown ) 15 replicateWord

    isGripperGripping ! ( playWorkspace cw_clearWorkspace ) ift 
  ) while
) "pickFromInputPile" store

( 
  ">>>> entering playWithObject <<<<" print
  comeToStop pressUntilEffortInit 0.03 setSpeed pressUntilEffort openGripper
  stay ( /* stay ensures that we actually rise */ ) "aComment" store
  0.01 setGridSize
  ( zUp ) scan_IRheight replicateWord
  comeToStop
  setScanModeCentered
  currentPose
  ( /* scanObjectStreamWaypointsIR */ ) "aComment" store
  catScan
) "playWithObject" store


( ( /* expects a pose on the stack */ ) "aComment" store
  moveEeToPoseWord
  waitUntilAtCurrentPosition

  0 "scan_pointCounter" store
  0 "scan_breakInd" store

  0.01 setGridSize ( xDown ) 20 replicateWord
  ( yDown ) 5 replicateWord

  ( scan_pointCounter 4 <   scan_breakInd not   and ) (
    ( yUp ) 10 replicateWord
    cw_viewIsClear (
      ( yDown ) 10 replicateWord
      cw_viewIsClear (
	( xUp ) 10 replicateWord
      ) (
	1 "scan_breakInd" store
      ) ifte
    ) (
      1 "scan_breakInd" store
    ) ifte

    scan_pointCounter 1 + "scan_pointCounter" store
  ) while

  scan_breakInd
) "scan_findObjectAfterDrop" store


( ( /* expects a pose on the stack */ ) "aComment" store
( /* pose */ ) "aComment" store
  clearClassLabels
  setScanModeCentered

  tenthImpulse
  moveEeToPoseWord 1 changeToHeight waitUntilAtCurrentPosition
  currentPose
( /* pose */ ) "aComment" store

  0 "scan_catScanCounter" store

  "catScan_" robotSerial leftOrRightArm dateString + + + "scan_catScanObjectName" store
  scan_catScanObjectName setScanGroup
  
  scan_findObjectAfterDrop "scan_catScanSees" store
  setBoundingBoxModeToMapping
  clearMapForPatrol
  1 changeToHeight waitUntilAtCurrentPosition
  synchronicServo 
  comeToStop pressUntilEffortInit 0.03 setSpeed pressUntilEffort 0.01 setGridSize ( zUp ) scan_IRheight replicateWord comeToStop 

( /* nothing on stack */ ) "aComment" store

  setPickModeToLearningAlgorithmC

  ( scan_catScanCounter 50 <   scan_catScanSees   and ) (
    currentPose

    endArgs
    currentPose
    1 changeToHeight scan_isNewComponent ! (
      scanObjectStreamWaypointsIR

      integrateImageStreamBufferCrops
      trainAndWriteFocusedClassKnn

      moveEeToPoseWord
      1 changeToHeight

      setPlaceModeToHold
      pickFocusedClass

      playWorkspace moveEeToPoseWord waitUntilAtCurrentPosition
      comeToStop pressUntilEffortInit 0.03 setSpeed pressUntilEffort 0.01 setGridSize ( zUp ) catScan_dropHeight replicateWord comeToStop 
      openGripper
  ( /* nothing on stack */ ) "aComment" store

      1 changeToHeight
      currentPose
      scan_findObjectAfterDrop "scan_catScanSees" store
  ( /* nothing on stack */ ) "aComment" store

      scan_catScanSees (
	setBoundingBoxModeToMapping
	clearMapForPatrol
	synchronicServo 
	comeToStop pressUntilEffortInit 0.03 setSpeed pressUntilEffort 0.01 setGridSize ( zUp ) scan_IRheight replicateWord comeToStop 
      ) ift
      scan_catScanCounter 1 + "scan_catScanCounter" store
  ( /* nothing on stack */ ) "aComment" store
    ) (
      pop pop pop
  ( /* nothing on stack */ ) "aComment" store
      pickFocusedClass

      isGripperGripping (
	playWorkspace moveEeToPoseWord
	comeToStop pressUntilEffortInit 0.03 setSpeed pressUntilEffort 0.01 setGridSize ( zUp ) catScan_dropHeight replicateWord comeToStop 
      ) ift
      openGripper
    )ifte
  ) while 

  createCachedClassifierFromClassLabels
  setPickModeToLearningAlgorithmC
  setPlaceModeToShake

  0 "scan_catPickCounter" store
  playWorkspace moveEeToPoseWord 1 changeToHeight currentPose
  scan_findObjectAfterDrop "scan_catScanSees" store

  ( scan_catPickCounter 20 <   scan_catScanSees   and ) (
    1 changeToHeight
    clearMapForPatrol clearBlueBoxMemories
    mapLocal
    pickClosestBlueBox

    playWorkspace moveEeToPoseWord 1 changeToHeight currentPose
    scan_findObjectAfterDrop "scan_catScanSees" store
    scan_catPickCounter 1 + "scan_catPickCounter" store
  ) while

) "catScan" store

(
  setPickModeToLearningAlgorithmC
  1 setSnapToFlushGrasp
  2 setGradientServoHardMaxIterations
  2 setGradientServoSoftMaxIterations

  1 "somethingsThere" store

  ( isGripperGripping ! somethingsThere and ) (
    ">>>> entering moveToOutputPile <<<<" print
    halfImpulse playWorkspace moveEeToPoseWord waitUntilAtCurrentPosition
    ( perturbPosition ) 5 replicateWord 
    openGripper
    halfImpulse
    1 changeToHeight
    setPlaceModeToHold

    ( /* check in this loop - if there are no blue boxes, it will hang */ ) "aComment" store
    ( /* instead, it should skip the rest of this function */ ) "aComment" store
    playWorkspace cw_workspaceIsClear ( 0 "somethingsThere" store ) ift

    "somethingsThere: " print somethingsThere print

    1 changeToHeight
    pickFocusedClass
    ( /* if you got it, lift it, if you didn't, try clearing the workspace */ ) "aComment" store
    ( /* this covers the case where the object is there but out of reach. */ ) "aComment" store
    isGripperGripping ( liftArm ) ( openGripper playWorkspace cw_clearWorkspace ) ifte
  ) while

  somethingsThere isGripperGripping and (
    halfImpulse outputPileWorkspace moveEeToPoseWord waitUntilAtCurrentPosition
    isGripperGripping ( comeToStop pressUntilEffortInit 0.03 setSpeed pressUntilEffort openGripper liftArm ) ift 
  ) ( openGripper ) ifte

  playWorkspace cw_clearWorkspace
) "moveToOutputPile" store

(
  1 setSnapToFlushGrasp
  2 setMapServoMode
  ( /* 4 setMapServoMode */ ) "aComment" store
  setPlaceModeToHold
  setBoundingBoxModeToMapping
  clearMapForPatrol
  openGripper

  ( 1 )
  (
    pickFromInputPile
    playWithObject
    moveToOutputPile 
  ) while

) "infiniteScan" store
